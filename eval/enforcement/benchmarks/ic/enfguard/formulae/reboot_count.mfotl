(*
  Policy: An alert is raised if there are more than 2 (unintended) reboots of IC hosts
  in the same data center within 30min windows. The first reboot is ignored, as every
  machine is (re)booted when a testnet starts.

  Relevant events:
  - originally_in_ic(node_id:string, node_addr:string)
  - registry__node_removed_from_ic(node_id:string, node_addr:string)
  - registry__node_added_to_ic(node_id:string, node_addr:string)
  - reboot(ip_addr:string, data_center_prefix:string)
  - reboot_intent(ip_addr:string, data_center_prefix:string)
*)

LET in_ic(node_id, node_addr) =
  ((*
      The node was originally in the IC, and it has not been removed.
    *)
    (ONCE originally_in_ic(node_id, node_addr)) AND NOT ONCE registry__node_removed_from_ic(node_id, node_addr)
  ) OR (
    (*
      The node was possibly not originally in the IC, but it has been added to it.
    *)
    ((NOT registry__node_removed_from_ic(node_id, node_addr)) SINCE registry__node_added_to_ic(node_id, node_addr))
  )
IN
LET true_reboot(ip_addr, data_center) =
  EXISTS node_id .
    in_ic(node_id, ip_addr)
    AND reboot(ip_addr, data_center)
    AND PREV ONCE reboot(ip_addr, data_center)
IN
LET unintended_reboot(ip_addr, data_center) =
  true_reboot(ip_addr, data_center)
  AND NOT PREV (NOT reboot(ip_addr, data_center) SINCE reboot_intent(ip_addr, data_center))
IN
LET num_reboots(data_center, n) =
  ONCE [0,30m) (EXISTS ip_addr. unintended_reboot(ip_addr, data_center)) AND
  n <- CNT (i; data_center;
    (ONCE [0,30m) (unintended_reboot(ip_addr, data_center) AND tp(i)))) 
IN
ALWAYS (
FORALL data_center, n .
  num_reboots(data_center, n) AND (n > 2)
  IMPLIES alert_reboots(data_center, n))
